{
    parserClass="org.intellij.sdk.language.parser.IlocParser"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Iloc"
    psiImplClassSuffix="Impl"
    psiPackage="org.intellij.sdk.language.psi"
    psiImplPackage="org.intellij.sdk.language.psi.impl"

    elementTypeHolderClass="org.intellij.sdk.language.psi.IlocTypes"
    elementTypeClass="org.intellij.sdk.language.psi.IlocElementType"
    tokenTypeClass="org.intellij.sdk.language.psi.IlocTokenType"

    tokens=[
        COLON=':'
        COMMA=','
        OP_1='=>'
        OP_2='->'

        string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"
        comment='regexp:(//.*)'
        var='regexp:@\w+'
        integer='regexp:\d+'
        id='regexp:\w+'
        space='regexp:\s+'
    ]
}

root ::= line*

line ::= !<<eof>> instruction

private instruction ::= (label COLON)? operation

label ::= id

private operation ::= function args? ((OP_1 args) | (OP_2 labels))?

function ::= id

private args ::= arg (COMMA arg)*

labels ::= label (COMMA label)*

private arg ::=  integer | string | var | register

register ::= id